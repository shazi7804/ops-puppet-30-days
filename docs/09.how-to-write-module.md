# Day 9 - 手把手系列 - 第一個 module

本系列文資料可參考以下：

- [Github](https://github.com/shazi7804/ops-puppet-30-days)
- [Gitbook](https://gitbook.com/book/shazi7804/puppet-manage-guide/details)
- [Mr.沙先生](https://shazi.info)

---

Module 是 Puppet 管理中非常重要的一環，利用已經編寫好的 module 在 manifests 就不需要在打上落落長的設定檔。

## 動手寫 Module

如果覺得線上的所有 module 都不符合你的需求，可以自己動手寫，在這篇會以基礎的 module 方向去寫。

你可以把 module 放在以下位置：

 - /etc/puppetlabs/code/environments/production/modules
 - /etc/puppetlabs/code/modules
 - /opt/puppetlabs/puppet/modules

### 基礎 module 檔案結構

以一個 apache module 為例，我會需要產生以下這些檔案結構

```shell
$ tree /etc/puppetlabs/code/modules/apache
├── files
|   └── default.conf
└── manifests
    ├── init.pp
    ├── install.pp
    ├── config.pp
    ├── service.pp
```

### module 的 manifests

init.pp 是預設被讀取的檔案，通常用來定義變數、引用 class。

```puppet
class apache (
  String $package_name      = 'apache2',
  String $package_ensure    = 'installed',
  String $service_name      = 'apache2',
  String $service_ensure    = 'running',
  String $default_site_conf = '/etc/apache2/sites-enabled/default.conf',
  String $run_user          = 'www-data',
){
  contain apache::install
  contain apache::config
  contain apache::service

  Class['::apache::install']
  -> Class['::apache::config']
  ~> Class['::apache::service']
}
```

install.pp 用來定義如何安裝 package。

```puppet
class apache::install inherits apache {
  package { $apache::package_name:
    ensure => $apache::package_ensure
  }
}
```

service.pp 用來處理服務

```puppet
class apache::service inherits apache {
  service { $apache::service_name:
    ensure    => $apache::service_ensure,
    subscribe => Package['apache'],
    require   => Class['apache::install']
  }
}
```

config.pp 用來處理設定檔

```puppet
class apache::config inherits apache {
  file { $apache::default_site_conf:
    ensure => file,
    owner  => $apache::run_user,
    source => "puppet:///modules/${module_name}/default.conf"
  }
}
```

## 怎麼引用 module

要引用 module 只需要使用 include 就可以把已經寫好的 apache module 引用進來

```puppet
node default {
  include ::apache
}
```
只要三行、三行、三行 !! 就完成佈署安裝 apache 了

這樣 Puppet code 就簡潔許多了，恭喜你已經學會了基本的 module 寫法，可以再往進階 module 前進。

[github]: https://github.com
[puppetforge]: https://forge.puppet.com/
[puppetlabs]: https://github.com/puppetlabs/
[voxpupuli]: https://github.com/voxpupuli
[puppet-nginx]: https://github.com/voxpupuli/puppet-nginx
[elastic]: https://github.com/elastic